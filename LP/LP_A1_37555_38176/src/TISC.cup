import java_cup.runtime.*;

import machine.*;
import machine.operations.*;

action code {:
  // creates an instance of the TISC
  TISC program = new TISC();
:} ;

parser code {:
  Yylex lexer;

  public void report_error(String message, Object error_info) {
    Symbol info = (Symbol) error_info;

    System.err.print("** " + message);
    System.err.println(" at " + info.value + "' in position " + info.left
		       + " **");
  }

  public void report_fatal_error(String message, Object empty) {
    System.err.println("** " + message);
  }
:} ;

init with {: lexer = new Yylex(System.in); :} ;

scan with {: return lexer.yylex(); :} ;

terminal        ADD, SUB, MULT, DIV, MOD, EXP ;
terminal        PUSH_INT, PUSH_VAR, STORE_VAR, PUSH_ARG, STORE_ARG ;
terminal        SET_ARG, CALL, LOCALS, RETURN ;
terminal        JUMP, JEQ, JLT ;
terminal        PRINT, PRINT_STR, PRINT_NL ;
terminal String IDENTIFIER ;
terminal String	INTEGER, STRING ;
terminal        TWO_POINTS;
terminal	ERROR ;

non terminal TISC program ;
non terminal      tag, instruction ;


program ::= program tag instruction
	       {: RESULT = program; :}
         |
	 ;

tag ::= IDENTIFIER:e TWO_POINTS
	 |
	 ;

instruction ::=
	      ADD
	        {: program.getOperationsList().add(new Add()); :}
	  |   SUB
	        {: program.getOperationsList().add(new Sub()); :}
	  |   MULT
            {: program.getOperationsList().add(new Mult()); :}
	  |   DIV
            {: program.getOperationsList().add(new Div()); :}
	  |   MOD
            {: program.getOperationsList().add(new Mod()); :}
	  |   EXP
            {: program.getOperationsList().add(new Exp()); :}
	  |   PUSH_INT INTEGER:i
	        {: program.getOperationsList().add(new PushInt(i)); :}
	  |   PUSH_VAR INTEGER:d INTEGER:n
	        {: program.getOperationsList().add(new PushVariables(d, n)); :}
	  |   PUSH_ARG INTEGER:d INTEGER:n
	        {: program.getOperationsList().add(new PushArguments(d, n)); :}
	  |   STORE_VAR INTEGER:d INTEGER:n
	        {: program.getOperationsList().add(new StoreVariables(d, n)); :}
	  |   STORE_ARG INTEGER:d INTEGER:n
	        {: program.getOperationsList().add(new StoreArguments(d, n)); :}
	  |   SET_ARG INTEGER:n
	        {: program.getOperationsList().add(new SetArguments(n)); :}
      |   CALL INTEGER:d IDENTIFIER:p
	        {: program.getOperationsList().add(new Call(d, p)); :}
	  |   LOCALS INTEGER:a INTEGER:v
            {: program.getOperationsList().add(new Locals(a, v)); :}
	  |   RETURN
            {: program.getOperationsList().add(new Return()); :}
	  |   JUMP IDENTIFIER:e
            {: program.getOperationsList().add(new Jump(e)); :}
	  |   JEQ IDENTIFIER:e
            {: program.getOperationsList().add(new Jeq(e)); :}
	  |   JLT IDENTIFIER:e
            {: program.getOperationsList().add(new Jlt(e)); :}
	  |   PRINT
            {: program.getOperationsList().add(new Print()); :}
	  |   PRINT_STR STRING:s
            {: program.getOperationsList().add(new PrintString(s)); :}
	  |   PRINT_NL
	        {: program.getOperationsList().add(new PrintNewLine()); :};
